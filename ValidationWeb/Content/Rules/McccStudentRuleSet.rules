ruleset McccStudentRuleSet

rule 50.02.0001
	when {MCCC_StudentSectionAssociation}.[SectionEnrollmentType] = 'ALC_IND_STUDY' then
	require that {MCCC_StudentSectionAssociation}.[IndependentStudyIndicator] = 1
	else 'This Student is associated with a Section with Enrollment Type of AI.  The Course offered in this section is missing the Independent Study Course Level Characteristic Indicator.'

rule 50.02.0002
	when {MCCC_StudentSectionAssociation}.[SectionEnrollmentType] = 'PROJECT_BASED' then
	require that {MCCC_StudentSectionAssociation}.[ProjectBasedLearningIndicator] = 1
	else 'This Student is associated with a Section with Enrollment Type of PB. The Course offered in this section is missing the Project Based Learning Course Level Characteristic Indicator.'
	
rule 50.02.0003
	when {MCCC_StudentSectionAssociation}.[AcademicSubject] exists then
	require that {MCCC_CourseGrades}.[StateCourseMultipleMarksIndicator] = 1
	else 'This Student has a Grade/Mark submitted with for an Academic Subject Area. The associated State course has no Multiple Marks indicator. Academic Subject Area should be removed from the reported grade.'
	
rule 50.02.0004
	when {MCCC_CourseGrades}.[StateCourseMultipleMarksIndicator] = 1 and {MCCC_CourseGrades}.[GradesReported] > 1 then
	require that {MCCC_CourseGrades}.[GradeAcademicSubject] exists
	else 'This Student has more than 1 Grade/Mark reported where Academic Subject for the Grade is not set, and the local course is a Multiple Marks course. Academic Subject Area is required.'
	
rule 50.02.0005
	when {MCCC_CourseGrades}.[StateCourseMultipleMarksIndicator] = 1 
		and {MCCC_CourseGrades}.[StateCourseCode] is in ['01055','01057'] 
		and {MCCC_CourseGrades}.[DistrictCourseOfferedGradeLevel] is in ['9','10','11','12']
		and {MCCC_CourseGrades}.[GradesReported] > 1 then
	require that {MCCC_CourseGrades}.[GradeAcademicSubject] = '01'
		or {MCCC_CourseGrades}.[GradeAcademicSubject] = '04'
	else 'This Student is enrolled in a District Course for Grade Levels 9-12 associated to State Course Classifications 01055 or 01057 with Multiple Marks = true, and more than one Student Section Grade has been reported. The Grade Academic Subject is missing or is not 01 or 04.'
	
rule 50.02.0006
	when {MCCC_CourseGrades}.[StateCourseMultipleMarksIndicator] = 1 
		and {MCCC_CourseGrades}.[StateCourseCode] is in ['01055','01057'] 
		and {MCCC_CourseGrades}.[DistrictCourseOfferedGradeLevel] is in ['KA','KB','KC','KD','KE','KF','KG','KH','KI','KJ','KK','1','2','3','4','5','6','7','8']
		and {MCCC_CourseGrades}.[GradesReported] > 1 then
	require that {MCCC_CourseGrades}.[GradeAcademicSubject] = '01'
		or {MCCC_CourseGrades}.[GradeAcademicSubject] = '02'
	else 'This Student is enrolled in a District Course for Grade Levels K-8 associated with a State Course with Multiple Marks = true. More than one Student Section Grade is reported but the Grade\'s Academic Subject Area is not language arts (1) or mathematics (2).'
	
rule 50.02.0007
	when {MCCC_CourseGrades}.[SectionMarkingIndicator] = 1 then
	require that {MCCC_CourseGrades}.[GradesReported] > 0
	else 'Student with Section Marking Indicator = true and without a Section Grade/Mark.'
	
rule 50.02.0008
	when {MCCC_CourseGrades}.[GradesReported] > 0 then
	require that {MCCC_CourseGrades}.[SectionMarkingIndicator] = 1 
	else 'Student Grade/Mark Reported with Section Marking Indicator = false.'
	
rule 50.02.0009
	when {MCCC_CourseGrades}.[CollegeCreditEarned] exists then
	require that {MCCC_CourseGrades}.[DistrictCourseLevelType] is in ['A','D','X']
	else 'Student with Section Grade/Mark with College Credit Earned for Local Course without Course Level Type of A, D or X.'
	
rule 50.02.0010
	when {MCCC_CourseGrades}.[DistrictCourseLevelType] is in ['A','D'] and {MCCC_CourseGrades}.[GradesReported] = 1 then
	require that {MCCC_CourseGrades}.[CollegeCreditEarned] exists
	else 'Course Level Type of A, D and Student with Section Grade/Mark with not College Credit Earned.'


	

